plugins {
	id 'dev.architectury.loom' version '0.11.0-SNAPSHOT'
	id 'maven-publish'
}

loom {
	forge {
		mixinConfigs = [
				"rubidium.mixins.json",
				"rubidium.compat.mixins.json"
		]
	}
	mixin.defaultRefmapName = "rubidium-mixins-refmap.json"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${project.archives_base_name}-mc${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

configurations {
	modIncludeImplementation

	include.extendsFrom modIncludeImplementation
	modImplementation.extendsFrom modIncludeImplementation
}

repositories {
	maven {
		name "Immersive Engineering maven"
		url "https://maven.blamejared.com"
	}
	maven {
		name = "CTM"
		url "https://maven.tterrag.com/"
		content {
			includeGroup "team.chisel.ctm"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

	modCompileOnly "blusunrize.immersiveengineering:ImmersiveEngineering:1.19.2-9.2.2-165"
	modCompileOnly "team.chisel.ctm:CTM:1.19.2-1.1.7+11"
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
