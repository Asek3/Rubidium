plugins {
	id 'dev.architectury.loom' version '0.10.0-SNAPSHOT'
	id 'maven-publish'
}

loom {
	silentMojangMappingsLicense()
	forge {
		mixinConfigs = [
				"rubidium.mixins.json",
				"rubidium.compat.mixins.json"
		]
	}
	mixin.defaultRefmapName = "rubidium-mixins-refmap.json"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "${project.archives_base_name}-mc${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "CTM"
		url "https://maven.tterrag.com/"
		content {
			includeGroup "team.chisel.ctm"
		}
	}
	maven {
		name = "CurseForge"
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

	modCompileOnly "curse.maven:flywheel-486392:3535459"
	modCompileOnly "curse.maven:codechickenlib-242818:3681973"
	modCompileOnly "team.chisel.ctm:CTM:MC1.16.1-1.1.2.6"
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = targetVersion
	}
}

java {
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
	}
}
